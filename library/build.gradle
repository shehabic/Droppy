apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 13
        targetSdkVersion 25
        versionCode 6
        versionName "0.6.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.shehabic.droppy'
    PUBLISH_ARTIFACT_ID = 'Droppy'
    PUBLISH_VERSION = '0.6.1'
}


// ./gradlew clean build generateRelease
apply plugin: 'maven'

def groupId = project.PUBLISH_GROUP_ID
def artifactId = project.PUBLISH_ARTIFACT_ID
def version = project.PUBLISH_VERSION

def localReleaseDest = "file:///android/maven"

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = artifactId
        pom.version = version
        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file://${localReleaseDest}")
    }
}

task zipRelease(type: Zip) {
    from localReleaseDest
    destinationDir buildDir
    archiveName "release-${version}.zip"
}

task generateReleases << {
    println "Release ${version} can be found at ${localReleaseDest}/"
    println "Release ${version} zipped can be found ${buildDir}/release-${version}.zip"
}

generateReleases.dependsOn(uploadArchives)
generateReleases.dependsOn(zipRelease)


artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
}













apply plugin: 'com.bmuschko.nexus'

modifyPom {
    project {
        name 'Droppy Popup Menu'
        packaging 'aar'
        description 'Cache library for Android applications'
        url 'https://github.com/shehabic/droppy'
        inceptionYear '2015'

        scm {
            url 'scm:git@github.com:shehabic/droppy.git'
            connection 'scm:git@github.com:shehabic/droppy.git'
            developerConnection 'scm:git@github.com:shehabic/droppy.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'shehabic'
                name 'Mohamed Shehab'
                email 'shehabic@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = false
    tests = true
    javadoc = false
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/services/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'http://oss.sonatype.org/content/repositories/snapshots'
}